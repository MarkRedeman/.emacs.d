(require 'semantic/lex)

(defconst ofc-semantic-php-number-regexp
  (eval-when-compile
    (concat "\\("
            "\\<[0-9]+[.][0-9]+\\([eE][-+]?[0-9]+\\)?[fFdD]?\\>"
            "\\|"
            "\\<[0-9]+[.][eE][-+]?[0-9]+[fFdD]?\\>"
            "\\|"
            "\\<[0-9]+[.][fFdD]\\>"
            "\\|"
            "\\<[0-9]+[.]"
            "\\|"
            "[.][0-9]+\\([eE][-+]?[0-9]+\\)?[fFdD]?\\>"
            "\\|"
            "\\<[0-9]+[eE][-+]?[0-9]+[fFdD]?\\>"
            "\\|"
            "\\<0[xX][0-9a-fA-F]+[lL]?\\>"
            "\\|"
            "\\<[0-9]+[lLfFdD]?\\>"
            "\\)"
            ))
  "Lexer regexp to match Java number terminals.
Following is the specification of Java number literals.

DECIMAL_LITERAL:
    [1-9][0-9]*
  ;
HEX_LITERAL:
    0[xX][0-9a-fA-F]+
  ;
OCTAL_LITERAL:
    0[0-7]*
  ;
INTEGER_LITERAL:
    <DECIMAL_LITERAL>[lL]?
  | <HEX_LITERAL>[lL]?
  | <OCTAL_LITERAL>[lL]?
  ;
EXPONENT:
    [eE][+-]?[09]+
  ;
FLOATING_POINT_LITERAL:
    [0-9]+[.][0-9]*<EXPONENT>?[fFdD]?
  | [.][0-9]+<EXPONENT>?[fFdD]?
  | [0-9]+<EXPONENT>[fFdD]?
  | [0-9]+<EXPONENT>?[fFdD]
  ;")

(defconst ofc-semantic-php-label-regex
  "[a-zA-Z_\x7f-\xff][a-zA-Z0-9_\x7f-\xff]*")

(defun ofc-semantic-php--move-to-php-beginning ()
  (if (re-search-forward "<[%?]" nil t)
      (cond
       ((or (looking-at "\\(php\\)?$")
	    (looking-at "\\(php\\)?[[:space:]])"))
	(goto-char (match-end 0))
	'T_NOTPHP)
       ((or (looking-at "=$")
	    (looking-at "=[[:space:]]"))
	'T_ECHO_BLOCK)
       (t
	(ofc-semantic-php--move-to-php-beginning)))
    (goto-char (point-max))
    nil))

;; NOTE I have now replaced this with a new
;; type of token called ns-separator which
;; is generated by automatically by wisent.
;; I may decide to deprecate this analyser.
(define-lex-regex-analyzer ofc-semantic-php-lex-ns-separator
  "Replace backslashes with a T_NS_SEPARATOR."
  "\\\\"
  (let ((start (point))
        (end   (match-end 0)))
    (semantic-lex-push-token
     (semantic-lex-token 'T_NS_SEPARATOR start end))))

(define-lex-regex-analyzer ofc-semantic-php-lex-prologue
  "Detects and converts a php opening tag to a T_OPEN_TAG token"
  "<[?%]\\(php\\)?\\([[:space:]]+\\|$\\)"
  ;; Zing to the end of this brace block.
  (let ((start (match-beginning 0))
        (end   (match-end 0)))
    (semantic-lex-push-token
     (semantic-lex-token 'T_OPEN_TAG start end))))

(define-lex-regex-analyzer ofc-semantic-php-lex-epilogue
  "Detects and converts a php closing tag to a T_CLOSE_TAG token"
  "[%?]>"
  (let ((start (match-beginning 0))
        (end   (match-end 0)))
    (semantic-lex-push-token
     (semantic-lex-token 'T_CLOSE_TAG start end))))

;; Define the lexer for this grammar
(define-lex ofc-semantic-php-lexer
  "Lexical analyzer that handles php buffers.
It ignores whitespaces, newlines and comments."
  ;; Ignore whitespace, newline and comments.
  semantic-lex-ignore-whitespace
  semantic-lex-ignore-newline
  semantic-lex-ignore-comments
  ofc-semantic-php-lex-prologue
  ofc-semantic-php-lex-epilogue
  ofc-semantic-php-wy--<number>-regexp-analyzer

  ofc-semantic-php-wy--<keyword>-keyword-analyzer
  ofc-semantic-php-wy--<symbol>-regexp-analyzer
  ofc-semantic-php-wy--<php-string>-sexp-analyzer
  ofc-semantic-php-wy--<ns-separator>-regexp-analyzer
  ofc-semantic-php-wy--<punctuation>-string-analyzer
  ofc-semantic-php-wy--<block>-block-analyzer
  ;; Throw a 'Unmatched Text during Lexical Analysis' error when no
  ;; lexical actions match the text.
  ;;
  ;; Comment the following line to receive more meaningful error when
  ;; testing.
  ;; semantic-lex-default-action)
  )

;; Taken from Emacs Cedet sources (semantic-grammar.el)
(defun ofc/semantic-php-lex-buffer ()
  "Run `ofc-semantic-lex' on current buffer."
  (interactive)
  (semantic-lex-init)
  (setq semantic-lex-analyzer 'ofc-semantic-php-lexer)
  (let ((token-stream
         (semantic-lex (point-min) (point-max))))
    (with-current-buffer (get-buffer-create "*ofc-semantic-php-lex*")
      (erase-buffer)
      (pp token-stream (current-buffer))
      (goto-char (point-min))
      (pop-to-buffer (current-buffer)))))

(provide 'ofc-semantic-php-wy-lex)

;;; ofc-semantic-wy-lex.el ends here.
